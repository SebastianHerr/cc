/**
 * C0-- Parser
 * HS-Fulda - Compiler Constructions
 *
 * @author    Konstantin Ruppel
 * @author    Juergen Goebel
 * @author    Sebastian Herr
 * 
 * @date      2017/06/03
 */

options
{
  DEBUG_PARSER = false;
  DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(C0)
import java.util.*;
import java.io.*;
import CMA.*;
public class C0 {
  
  static AppearanceTable vidTable;
  static AppearanceTable sidTable;
  static AppearanceTable fidTable;
  
  public static void main(String args[]) {
    C0 parser;
    NodeRoot ast;  
    vidTable = new AppearanceTable();
    sidTable = new AppearanceTable();
    fidTable = new AppearanceTable();
    
    String filename = "bin/code.cma";
    
    try {
      if (args.length > 0) {
        filename = args[0];
        parser = new C0 (new java.io.FileInputStream(new java.io.File(filename)));
      } else {
        parser = new C0 (System.in);
      }
      
      System.out.println ("/*");
      
      //Parse
      ast = parser.Program();
      System.out.println ("[  OK  ] Parse complete");
      
      //Link Symbols to definitions
      vidTable.addAllSymbolsToScopeNodes();
      System.out.println ("[  OK  ] Symboltable complete");
      
      //Type checking
      if(ast.checkNodeType() == false)
      {
        throw new Exception("Type");
      }
      System.out.println ("[  OK  ] Typechecking complete");
      
      String binaryCode = ast.emitCode();
      
      BufferedWriter out = new BufferedWriter(new FileWriter(filename + ".cma"));
      out.write(binaryCode);
      out.close();
      System.out.println ("[  OK  ] Code generation complete");
      System.out.println ("\tSaved the code to:" + filename + ".cma");
      
      
      //temporarly hide the system.out, because the cma code would spam too much
      PrintStream originalStream = System.out;
      PrintStream dummyStream    = new PrintStream(new OutputStream(){public void write(int b) {/*NO-OP*/}});
      System.setOut(dummyStream);
      
      int cmaResult;
      try
      {
        cmaResult = Main.run(binaryCode);      
      }
      catch(Exception ex)
      {
        throw new CodeExecutionException(ex);
      }
      
      //restore system.out
      System.setOut(originalStream);

      //Checking return value
      int count = filename.length() - filename.replace("_", "").length();
      //Only test with 3 underscores should have the return value tested
      if(count == 3)
      {
        String[] parts = filename.split("_");
        int supposedValue = Integer.parseInt(parts[2]);
        System.out.println("Value is supposed to be " +supposedValue + " and is " + cmaResult);
        System.out.println ("[  OK  ] Code running complete");
      }
      
      
      
      //Output all debug data
      //System.out.println ("Stats of vidTable");
      //vidTable.printStats();
      //System.out.println ("Stats of sidTable");
      //sidTable.printStats();
      //System.out.println ("Stats of fidTable");
      //fidTable.printStats();
      System.out.println (" */");
      //System.out.println(ast);
			System.out.println ("/*\n[  OK  ]\n*/");
      System.exit(0);
		} 
    catch (ParseException ex) 
    {
			System.out.println ("[ FAIL PARSE ]\n" + ((args.length > 1) ? ex.getMessage() + "\n\n" : ""));
      System.exit(1);
		} 
    catch (TokenMgrError ex)
    {
      System.out.println ("[ FAIL TOKEN]\n" + ((args.length > 1) ? ex.getMessage() + "\n\n" : ""));
      System.exit(2);
    } 
    catch (SymbolTableGenerationException ex)    
    {
      System.out.println ("[ FAIL SYMBOLTABLE]\n" + ((args.length > 1) ? ex.getMessage() + "\n\n" : ""));
      System.exit(3);
    }  
    catch (TypeCheckingException ex)    
    {
      System.out.println ("[ FAIL TYPECHECKING]\n" + ((args.length > 1) ? ex.getMessage() + "\n\n" : ""));
      System.exit(4);
    } 
    catch (CodeGenerationException ex)
    {
      System.out.println ("[ FAIL CODEGENERATION]\n" + ((args.length > 1) ? ex.getMessage() + "\n\n" : ""));
      System.exit(5);
    }
    catch (Exception ex)
    {
      System.out.println(ex);
      ex.printStackTrace();
      System.out.println("UNKNOWN ERROR\n" + ((args.length > 1) ? ex.getClass() + "" + ex.getMessage() + "\n\n" : ""));
      System.exit(666);
    }
	}
}
PARSER_END(C0)

TOKEN_MGR_DECLS:
{
  static int commentDepth;
}

SKIP : 
{ 
  " " | "\t" | "\f" | "\n" | "\r" | "\r\n"
  |
  "/*" {commentDepth = 1;} : IN_ML_COMMENT
  |
  < "//" (~["\n","\r"])* ("\n" | "\r" | "\r\n") >
}

/*
 * Allowing for nested multiline comments
 * Implementation from Introduction to "Compiler Construction in a Java World" page 56
 */
<IN_ML_COMMENT> SKIP :
{
  <~[]>
  |
  "*/" {commentDepth -= 1; SwitchTo( commentDepth == 0 ? DEFAULT : IN_ML_COMMENT);}
  |
  "/*" {commentDepth += 1;}
}

// Keywords
TOKEN : {
	< INT:        	"int" >
	| < BOOLEAN:    "bool" >
	| < CHAR:       "char" >
	| < VOID:       "void" >
	| < STRUCT:     "struct" >
	| < IF:         "if" >
	| < ELSE:       "else" >
	| < WHILE:      "while" >
	| < FOR:        "for" >
	| < RETURN:     "return" >
	| < TRUE:       "true" >
	| < FALSE:      "false" >
	| < NULL:       "NULL" >
	| < ALLOC:      "alloc" >
  | < ALLOC_ARR:  "alloc_array" >
}

TOKEN : {
  < ID: <LETTER> ( <ALPHANUM> )* >
  |	< #LETTER: ["$","A"-"Z","a"-"z","_"] >
  |	< #ALPHANUM: ["$","A"-"Z","a"-"z","0"-"9","_"] >
  |	< NUM: <DECNUM> | <HEXNUM> >
  |	< DECNUM: "0" | ["1"-"9"](["0"-"9"])* >
  |	< HEXNUM: "0" ("x" | "X") (["0"-"9","a"-"f","A"-"F"])+ >
}

NodeRoot Program () : {NodeRoot root = new NodeRoot(); Node node;}
{
  
  (
    ( 
      node = Declaration_Definition() {root.addDeclarationOrDefinition(node);}
    )* 
    <EOF> 
  )

  {return root;}
}

NodeIdentifier Sid () : {Token name; NodeIdentifier node;}
{
   name = <ID> {node = new NodeIdentifier(name,IdentifierType.SID); sidTable.addSymbol(node); return node;}   // struct name
}

NodeIdentifier Vid () : {Token name; NodeIdentifier node;}
{
   name = <ID> {node = new NodeIdentifier(name,IdentifierType.VID); vidTable.addSymbol(node); return node;}   // variable and function name
}

NodeIdentifier Fid () : {Token name; NodeIdentifier node;}
{
  name = <ID> {node = new NodeIdentifier(name,IdentifierType.FID); fidTable.addSymbol(node); return node;} // field name
}

NodeFunctionParamArgs FunctionParamArgs () : {NodeFunctionParamArgs result; Node type; Node name;}
{
  {result = new NodeFunctionParamArgs();}

  [ 
    type = Type() 
    name = Vid()  {result.addParameter(type, name);}
    ( 
      "," type = Type() 
      name = Vid() 
      {result.addParameter(type, name);} 
    )* 
  ]

  {return result;}
}

Node Declaration_Definition () : {Node result = null; Node type; NodeIdentifier name; NodeBlock nodeFunctionCode;
  NodeStruct resultStruct; NodeFunction resultFunction; NodeFunctionParamArgs newParams;}
{
  (
    LOOKAHEAD (3)
    <STRUCT> name = Sid() {resultStruct = new NodeStruct(name);}
      [ 
        // [ ] Warum hier Klammer nicht tokenized?
        "{" 
        ( type=Type() name=Fid() { resultStruct.addElement(type, name);}";")* "}" 
      ] 
      ";" {result = resultStruct;}
    |
    type=Type() name=Vid() {resultFunction = new NodeFunction(type, name);}
    "(" 
    newParams = FunctionParamArgs() {resultFunction.addParameters(newParams);} 
    ")" 
    ( 
      nodeFunctionCode=Block() {resultFunction.setFunctionCodeBlock(nodeFunctionCode);}  
      | 
      ";"
    ) {result = resultFunction;} 
  )
  {return result;}
}

NodeBlock Block() : {NodeBlock result; Node newNode; Token token;}
{
  // [ ] Warum hier Klammer tokenized?
  token = "{" {result = new NodeBlock(token);} 
  ( 
    newNode=Statement() {result.addStatement(newNode);}
  )* 
  "}"
  {return result;}
}

Node Statement () : {Node result = null; NodeStatementIf resultIf;
  NodeStatementWhile resultWhile; NodeStatementFor resultFor;
  NodeStatementReturn resultReturn; Node newNode = null; Token token;}
{
  (
    newNode = Expression() 
    ";" {result=new NodeStatementExpression(newNode);}
    |
    token = <IF> 
    "(" 
    newNode = Expression() {resultIf=new NodeStatementIf(newNode,token);}
    ")" 
    newNode=Statement() {resultIf.setIf(newNode);}
    [ 
      LOOKAHEAD(2) 
      <ELSE> 
      newNode = Statement() {resultIf.setElse(newNode);}
    ] {result=resultIf;}
    |
    token = <WHILE> 
    "(" 
    newNode = Expression() {resultWhile=new NodeStatementWhile(newNode, token);}
    ")" 
    newNode = Statement() {resultWhile.setLoopBody(newNode);result=resultWhile;}
    |
    token = <FOR> {resultFor=new NodeStatementFor(token);} 
    "(" 
    [ 
      newNode=Expression() {resultFor.setInitializer(newNode);}
    ] 
    ";" newNode=Expression() {resultFor.setCondition(newNode);}
    ";" 
    [ 
      newNode=Expression() {resultFor.setIterator(newNode);}
    ] 
    ")" newNode=Statement()
    {resultFor.setLoopBody(newNode);result=resultFor;}
    |
    token = <RETURN> {resultReturn=new NodeStatementReturn(token);}
    [ 
      newNode=Expression() {resultReturn.setReturnValue(newNode);} 
    ] 
    ";" {result=resultReturn;}
    |
    result = Block() //No need to create a seperate statment block node, everything is already in the block
  )
  {return result;}
}

NodeMemberAccess MemberAccess () : {Node newNode; NodeMemberAccess memberaccess;Token token;}
{
  (
    token = "." newNode = Fid() {memberaccess = new NodeMemberAccessDot(newNode, token);}
    |
    token = "->" newNode = Fid() {memberaccess = new NodeMemberAccessArrow(newNode, token);}
    |
    token = "[" newNode = Expression() "]" {memberaccess = new NodeMemberAccessArray(newNode, token);}
  )
  [
    newNode = MemberAccess() {memberaccess.setAccessingNode(newNode);} 
  ]
  {return memberaccess;}
}

Node Type () : {Node result; Node newNode;Token token;}
{
	(
		token = <INT> {result = new NodeTypeInt(token);} 
    | 
    token = <BOOLEAN> {result = new NodeTypeBool(token);}
    | 
    token = <VOID> {result = new NodeTypeVoid(token);}
		|
		token = <STRUCT> newNode = Sid() {result = new NodeTypeStruct(newNode);}
	)
	( 
		"*" {result = new NodeTypePointer(result);}
    | 
    "["
    "]" {result = new NodeTypeArray(result);}
	)*
  {return result;}
}

NodeFunctionCallArgs FunctionCallArgs () : {NodeFunctionCallArgs result; Node newNode;}
{
  {result = new NodeFunctionCallArgs();}
  [ 
    newNode = Expression() {result.addArg(newNode);}
    (
      "," newNode = Expression() {result.addArg(newNode);}
    )* 
  ]
  {return result;}
}

Node Expression () : {Node result; Node array_alloc_size; Token token;
  Node type; NodeIdentifier name; Node newNode;NodeExpressionVariableDefinition resultDefine;}
{	
  token=<ALLOC> 
  "(" result=Type() 
  ")" {return new NodeExpressionAlloc(result,token);}
  |
  token=<ALLOC_ARR> 
  "(" result=Type() 
  "," array_alloc_size=Expression() 
  ")" {return new NodeExpressionAllocArray(result,token,array_alloc_size);}
  |
  result=OpRang_Assign() {return result;}
  |
  type=Type() 
  name=Vid() {resultDefine = new NodeExpressionVariableDefinition(type,name);result=resultDefine; }
  [ 
    LOOKAHEAD(2)
    token = "=" 
    newNode=OpRang_Conditional() {resultDefine.setAssignment(newNode);}
  ]
  {if(result==null){System.out.println("result null for some reason");}else{return result;}}
}

Node OpRang_Assign () : {Node result; NodeExpressionBinary operator = null; NodeExpressionAssign resultAssign; Token opToken;}
{
  result=OpRang_Conditional()
  [ 
    LOOKAHEAD(2)
    {resultAssign= new NodeExpressionAssign(result);}
    (
      opToken="="  | 
      opToken="+=" {operator = new NodeExpressionSum(result);      operator.setOp("+", opToken);}|    //Untangle "a += b;" to "a = a + b"
      opToken="-=" {operator = new NodeExpressionSum(result);      operator.setOp("-", opToken);}| 
      opToken="*=" {operator = new NodeExpressionFactor(result);   operator.setOp("*", opToken);}| 
      opToken="/=" {operator = new NodeExpressionFactor(result);   operator.setOp("/", opToken);}| 
      opToken="%=" {operator = new NodeExpressionFactor(result);   operator.setOp("%", opToken);}| 
      opToken="<<="{operator = new NodeExpressionBitShift(result); operator.setOp("<<", opToken);}| 
      opToken=">>="{operator = new NodeExpressionBitShift(result); operator.setOp(">>", opToken);}| 
      opToken="&=" {operator = new NodeExpressionBitAnd(result);   operator.setOp("&", opToken);}| 
      opToken="^=" {operator = new NodeExpressionBitXor(result);   operator.setOp("^", opToken);}| 
      opToken="|=" {operator = new NodeExpressionBitOr(result);    operator.setOp("|", opToken);}
    ) 
    result=OpRang_Conditional() {resultAssign.setOp("=", opToken);
                                if(operator == null){resultAssign.setSecondary(result);} 
                                else{operator.setSecondary(result); resultAssign.setSecondary(operator);}
                                result=resultAssign;}
  ]
  {return result;}
}

Node OpRang_Conditional () : {Node result; NodeStatementIf resultIf; Token token;}
{
  //Using the normal If Node since this here is just a shorthand for the normal if then else construct
  result = OpRang_LogicalOr() 
  [ 
    LOOKAHEAD(2)  
    ( 
      token = "?" {resultIf = new NodeStatementIf(result, token);} 
      result=Expression() {resultIf.setIf(result);}
      ":" result=Expression() {resultIf.setElse(result); result = resultIf;}
    ) 
  ]
  {return result;}
}

Node OpRang_LogicalOr () : {Node result; NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_LogicalAnd() 
  [ 
    {resultBin = new NodeExpressionLogicalOr(result);} 
    ( // [?] Warum geklammert?
      opToken = "||" 
    ) 
    result=OpRang_LogicalOr()  {resultBin.setOp("||",opToken); resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_LogicalAnd () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_BitOr() 
  [ 
    {resultBin = new NodeExpressionLogicalAnd(result);} 
    ( // [?] Warum geklammert?
      opToken="&&" 
    ) 
    result=OpRang_LogicalAnd()  {resultBin.setOp("&&",opToken);resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_BitOr () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_BitXor() 
  [ 
    {resultBin = new NodeExpressionBitOr(result);} 
    ( 
      opToken="|" 
    ) 
    result=OpRang_BitOr()  {resultBin.setOp("|",opToken);resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_BitXor () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_BitAnd() 
  [ 
    {resultBin = new NodeExpressionBitXor(result);} 
    ( 
      opToken="^" 
    ) 
    result=OpRang_BitXor()  {resultBin.setOp("^",opToken);resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_BitAnd () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_Equality() 
  [
    {resultBin = new NodeExpressionBitAnd(result);}
    ( 
      opToken="&" 
    ) 
    result=OpRang_BitAnd() {resultBin.setOp("&",opToken);resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_Equality () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_Compare() 
  [
    {resultBin = new NodeExpressionEquality(result);} 
    ( 
      opToken="==" {resultBin.setOp("==",opToken);}
      | 
      opToken="!=" {resultBin.setOp("!=",opToken);}
    ) 
    result=OpRang_Equality() {resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_Compare () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_BitShift() 
  [
    {resultBin = new NodeExpressionCompare(result);} 
    ( 
      opToken="<" {resultBin.setOp("<",opToken);}
      | 
      opToken=">" {resultBin.setOp(">",opToken);}
      | 
      opToken=">=" {resultBin.setOp(">=",opToken);}
      | 
      opToken="<=" {resultBin.setOp("<=",opToken);}
    ) 
    result=OpRang_Compare() {resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_BitShift () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_Sum() [ {resultBin = new NodeExpressionBitShift(result);} 
        ( opToken="<<" {resultBin.setOp("<<",opToken);}| 
          opToken=">>" {resultBin.setOp(">>",opToken);}) 
        result=OpRang_BitShift() {resultBin.setSecondary(result);result=resultBin;}]
  {return result;}
}

Node OpRang_Sum () : {Node result;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_Factor() 
  [ 
    {resultBin = new NodeExpressionSum(result);} 
    ( 
      opToken="+" {resultBin.setOp("+",opToken);}
      | 
      opToken="-" {resultBin.setOp("-",opToken);}
    ) 
    result=OpRang_Sum() {resultBin.setSecondary(result);result=resultBin;}
  ]
  {return result;}
}

Node OpRang_Factor () : {Node result; Node newNode;NodeExpressionBinary resultBin;Token opToken;}
{
  result=OpRang_Unary() 
  [ 
    {resultBin = new NodeExpressionFactor(result);} 
    (
      opToken="*" {resultBin.setOp("*",opToken);} 
      |
      opToken="/" {resultBin.setOp("/",opToken);} 
      | 
      opToken="%" {resultBin.setOp("%",opToken);} 
    ) 
    newNode=OpRang_Factor() {resultBin.setSecondary(newNode);result=resultBin;}
  ] 
  {return result;}
}

Node OpRang_Unary () : {NodeExpressionUnary unaryOp = null; Node result; 
  NodeMemberAccess memberaccess = null; NodeFunctionCallArgs functionArgs; Token token;}
{
  //gather information for this node...
  [
    token="*" {unaryOp=new NodeExpressionUnaryDereference(token);} 
    | 
    token="-" {unaryOp=new NodeExpressionUnaryMinus(token);} 
    | 
    token="~" {unaryOp=new NodeExpressionUnaryBitwiseNegation(token);} 
    |
    token="!" {unaryOp=new NodeExpressionUnaryNegation(token);}
  ]
  (
    result=Vid() 
    [ 
      "(" 
      functionArgs=FunctionCallArgs() 
      ")" {result = new NodeExpressionFunctionCall(result, functionArgs);} 
      | 
      token="--" { result = new NodeExpressionDecrement((NodeIdentifier)result, token);}
      | 
      token="++" { result = new NodeExpressionIncrement((NodeIdentifier)result, token);}
    ]
    |
    token=<NUM>  {result= new NodeExpressionConstNum(token);}	
    | 
    token=<TRUE> {result= new NodeExpressionConstBoolean(token, true);} 
    | 
    token=<FALSE>{result= new NodeExpressionConstBoolean(token, false);} 
    | 
    token=<NULL> {result= new NodeExpressionConstNull(token);}
    |
    token="(" 
    result=OpRang_Conditional() {result = new NodeExpressionBrackets(result, token);} 
    ")"
  )
  [
    /*
     * If there is a constant or function before that, then the type checker will complain here,
     * unless the returned item is a stack or something :)
     */
    memberaccess=MemberAccess()
  ]
  //...and assemble it
  {
    if(memberaccess != null)
    { memberaccess.setAccessingNode(result); result=memberaccess; }
    if(unaryOp != null)
    { unaryOp.setInternNode(result); result=unaryOp; }
    return result;
  }
}
